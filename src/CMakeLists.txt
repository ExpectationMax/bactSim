CMAKE_MINIMUM_REQUIRED(VERSION 2.8)
project(chemohybrid-gpu)

# HDF5 dependency for saving and loading
FIND_PACKAGE(HDF5Cpp REQUIRED)
INCLUDE_DIRECTORIES(${LIBHDF5_INCLUDE_DIR})
ADD_DEFINITIONS(${HDF5_DEFINITIONS})
#set(LIBRARIES ${HDF5_CXX_LIBRARIES})

# Find the ArrayFire package.
FIND_PACKAGE(ArrayFire REQUIRED)
# If ArrayFire is found, the following variables will be defined:
#
# ArrayFire_INCLUDE_DIRS    - Location of ArrayFire's include directory.
# ArrayFire_LIBRARIES       - Location of ArrayFire's libraries. This will default
#                             to a GPU backend if one is found.
# ArrayFire_FOUND           - True if ArrayFire has been located
#
# You may provide a hint to where ArrayFire's root directory may be located
# by setting ArrayFire_DIR.
#
# ----------------------------------------------------------------------------
#
# ArrayFire_CPU_FOUND        - True of the ArrayFire CPU library has been found.
# ArrayFire_CPU_LIBRARIES    - Location of ArrayFire's CPU library, if found
# ArrayFire_CUDA_FOUND       - True of the ArrayFire CUDA library has been found.
# ArrayFire_CUDA_LIBRARIES   - Location of ArrayFire's CUDA library, if found
# ArrayFire_OpenCL_FOUND     - True of the ArrayFire OpenCL library has been found.
# ArrayFire_OpenCL_LIBRARIES - Location of ArrayFire's OpenCL library, if found

# Include the ArrayFire hreaders
INCLUDE_DIRECTORIES(${ArrayFire_INCLUDE_DIRS})
INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR})

# Get a list of all of the source files in the directory:
#FILE(GLOB SOURCE "Environments/*.cpp")
set(NO_GRAPHICS FALSE CACHE BOOL "Do not compile with visualization")

if(NO_GRAPHICS)
    add_definitions(-DNO_GRAPHICS)
endif()

set(GENERAL General/Types.h General/CoordinateIndexer.cpp General/CoordinateIndexer.h General/StorageHelper.h General/StorageHelper.cpp General/Ligand.cpp General/Ligand.h General/ArrayFireHelper.cpp General/ArrayFireHelper.h) # General/StorageManager.h General/StorageManager.cpp )
set(ENVIRONEMENTS
        Environments/BoundaryCondition.h Environments/BoundaryCondition.cpp
        Environments/EnvironmentBase.h Environments/EnvironmentBase.cpp
        Environments/Environment.h Environments/Environment.cpp
        Environments/ConstantEnvironment.h
        )
set(BACTERIA BacterialPopulations/BacterialPopulation.cpp BacterialPopulations/SimplePopulation.cpp BacterialPopulations/Matthaeus2009Population.cpp)
set(SOLVERS Solvers/Solver.cpp Solvers/RungeKuttaSolver.cpp Solvers/ForwardEulerSolver.cpp)
set(MODELS Models/Model2D.h Models/Model2D.cpp )
set(SOURCE ${GENERAL} ${ENVIRONEMENTS} ${BACTERIA} ${SOLVERS} ${MODELS})

# There are several ways of compiling source code in CMake. In most cases you
# specify the source files to an ADD_EXCUTABLE call. Because we intend to
# link against multiple ArrayFire backends, this approach would result in
# every source file being re-compiled multiple times.
# Here we take a different approach to compilation. We build all of the source
# files into a CMake object library and then link this library against the
# different ArrayFire backends.
ADD_LIBRARY(LIB OBJECT ${SOURCE})
ADD_LIBRARY(AF_SIMULATE OBJECT simulate.cpp)

# Build the program, linking specifically with designated backends
# ArrayFire CPU backend
if(${ArrayFire_CPU_FOUND})
    MESSAGE(STATUS "ArrayFire CPU backend found. Enabling CPU benchmark")
    ADD_EXECUTABLE(simulate-cpu $<TARGET_OBJECTS:AF_SIMULATE> $<TARGET_OBJECTS:LIB>)
    TARGET_LINK_LIBRARIES(simulate-cpu ${LIBHDF5_LIBRARIES} ${ArrayFire_CPU_LIBRARIES}
            ${CMAKE_THREAD_LIBS_INIT})
ENDIF()

# ArrayFire OpenCL backend
FIND_PACKAGE(OpenCL)
IF(${ArrayFire_OpenCL_FOUND} AND ${OpenCL_FOUND})
    # We need to find OpenCL as transitive linking is disabled on some OSes
    MESSAGE(STATUS "ArrayFire OpenCL backend found. Enabling OpenCL benchmark")
    ADD_EXECUTABLE(simulate-opencl $<TARGET_OBJECTS:AF_SIMULATE> $<TARGET_OBJECTS:LIB>)
    TARGET_LINK_LIBRARIES(simulate-opencl ${LIBHDF5_LIBRARIES} ${ArrayFire_OpenCL_LIBRARIES}
            ${OpenCL_LIBRARIES} ${CMAKE_THREAD_LIBS_INIT} )
ENDIF()

# ArrayFire CUDA backend
FIND_PACKAGE(CUDA)
IF(${ArrayFire_CUDA_FOUND} AND ${CUDA_FOUND})
    # We need to find CUDA and NVVM as transitive linking is disabled on some OSes
    FIND_PACKAGE(CUDA REQUIRED)
    FIND_PACKAGE(NVVM REQUIRED)
    MESSAGE(STATUS ${CUDA_TOOLKIT_ROOT_DIR})
    MESSAGE(STATUS "ArrayFire CUDA found. Enabling CUDA benchmark")
    ADD_EXECUTABLE(simulate-cuda $<TARGET_OBJECTS:AF_SIMULATE> $<TARGET_OBJECTS:LIB>)
    TARGET_LINK_LIBRARIES(simulate-cuda ${LIBHDF5_LIBRARIES} ${ArrayFire_CUDA_LIBRARIES}
            ${CMAKE_THREAD_LIBS_INIT} ${CUDA_LIBRARIES} ${NVVM_LIB})
ENDIF()

add_subdirectory(Examples)